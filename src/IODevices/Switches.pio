.program switches_pio
.side_set 4 opt

    ; Initialize X to all pins HIGH. Max. 16 switches, so 16 bit, so 0xFFFF:
    ; But "set" can only set 5 bits, so a max value of 31 (0x1F).
    set x, 31        ; X = 0x1F
    in x, 5          ; shift 5 bits from X into ISR, now ISR = 0x1F
    in x, 5          ; shift 5 bits from X into ISR, now ISR = 0x3FF
    in x, 5          ; shift 5 bits from X into ISR, now ISR = 0x7FFF
    in x, 1          ; shift 1 bit from X into ISR, now ISR = 0xFFFF
    mov x, isr       ; copy ISR to X, now X = 0xFFFF
    set y, 0         ; Y = 0x0

loop:
    mov isr, y       ; ISR = 0x0
    in pins, 16      ; read 16 switches to ISR (filling lower 16 bits of the 32bit ISR)
    mov y, isr       ; copy ISR to Y for comparison
    jmp x!=y changed ; jump to "changed" if X (old state) != Y (new state)
    jmp loop

changed:
    mov osr, y
    push block       ; push OSR to FIFO, "block" waits until the CPU handled enough data in the FIFO, so we don't overflow it
    irq 0            ; trigger interrupt 0 to notify the main CPU that a switch state has changed
    mov x, y         ; update X to the new switch states
    set y, 0         ; Y = 0x0

    ; Reset stateful pins (GPIO 15-18)
    set pindirs, 0b1111     side 0      ; change direction of 4 pins (15-18) to output, set LOW
    nop                     side 0b1111 ; set 4 pins to HIGH
    nop                     side 0b1111 ; short delay
    nop                     side 0b1111 ; short delay
    nop                     side 0b1111 ; short delay
    nop                     side 0      ; set LOW
    set pindirs, 0          side 0      ; change direction all pins back to input

    jmp loop
